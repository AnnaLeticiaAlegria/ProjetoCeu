//#include "wclock.ceu"
#include "gpio.ceu"

output bool PIN_03; //BUZZER

output bool PIN_13; /*LED1*/
output bool PIN_12; /*LED2*/
output bool PIN_11; /*LED3*/
output bool PIN_10; /*LED4*/

output bool PIN_04; //LATCH_DIO
output bool PIN_07; //CLK_DIO
output bool PIN_08; //DATA_DIO

native _shiftOut;
native _MSBFIRST;

// Segment byte maps for numbers 0 to 9 
var[10] byte segment_map = [ 0xC0, 0xF9, 0xA4, 0xB0, 0x99, 0x92, 0x82, 0xF8, 0X80, 0X90 ];
// Byte maps to select digit 1 to 4 
var[4] byte segment_select = [ 0xF1, 0xF2, 0xF4, 0xF8 ];

//para os LEDs comeÃ§arem apagados
emit PIN_10(true);
emit PIN_11(true);
emit PIN_12(true);
emit PIN_13(true);
emit PIN_03(true);

var[4] int horario = [ 2,3,5,9 ];
var[4] int alarme = [ 0,0,0,0 ];
var bool alarmChange = false;

input (int,bool) PIN_;
code/await Pin_ (var int pin) -> NEVER do
    { pinMode(@pin, INPUT); }
    var bool v1 = _digitalRead(pin) as bool;
    loop do
        await 50ms;
        var bool v2 = _digitalRead(pin) as bool;
        if v1 != v2 then
            v1 = v2;
            await async (pin,v1) do
                emit PIN_(pin,v1);
            end
        end
    end
end

spawn Pin_(_A1);
spawn Pin_(_A2);
spawn Pin_(_A3);

code/await Display (var&[4] int v, var&[4] int vAlarme) -> NEVER do
    loop do
        var int i;
        loop i in [0 -> 4[ do
            emit PIN_04(false);
            if outer.alarmChange == false then
              _shiftOut ( 8, 7, _MSBFIRST, outer.segment_map[v[i]] );
            else
              _shiftOut ( 8, 7, _MSBFIRST, outer.segment_map[vAlarme[i]] );
            end
            _shiftOut ( 8, 7, _MSBFIRST, outer.segment_select[i] );
            emit PIN_04(true);
        end
        await async do end;
        //await 5ms; //enquanto nao tem o async
    end
end

code/call AumentaHora (var&[4] int vHorario) -> none do
    if vHorario[1] < 9 then
        vHorario[1] = vHorario[1] + 1;
    else
        vHorario[1] = 0;
        vHorario[0] = vHorario[0] + 1;
    end
    if vHorario[0] == 2 and vHorario[1] == 4 then
        vHorario[0] = 0;
        vHorario[1] = 0;
    end
end

code/call AumentaMinuto (var&[4] int vHorario) -> none do
    if vHorario[3] < 9 then
        vHorario[3] = vHorario[3] + 1;
    else
        vHorario[3] = 0;
        vHorario[2] = vHorario[2] + 1;
    end
    if vHorario[2] > 5 then
        call AumentaHora(&vHorario);
        vHorario[2] = 0;
    end
end

code/call DiminuiHora (var&[4] int vHorario) -> none do
    if vHorario[1] > 0 then
        vHorario[1] = vHorario[1] - 1;
    else
        vHorario[1] = 9;
        if vHorario [0] > 0 then
            vHorario[0] = vHorario[0] - 1;
        else
            vHorario[0] = 2;
            vHorario[1] = 3;
        end
    end
end

code/call DiminuiMinuto (var&[4] int vHorario) -> none do
    vHorario[3] = vHorario[3] - 1;
    if vHorario[3] < 0 then
        vHorario[2] = vHorario[2] - 1;
        vHorario[3] = 9;
    end
    if vHorario[2] < 0 then
        call DiminuiHora(&vHorario);
        vHorario[2] = 5;
        vHorario[3] = 9;
    end
end

code/await Segurou_botao (var int botao) -> none do
    var int pin;
    var bool v;
    loop do
        (pin,v) = await PIN_ until pin==botao and v==false;
        par/or do
            (pin,v) = await PIN_ until pin==botao and v==true;
        with
            await 2s;
            break;
        end
    end
end


code/await Pisca (var int pin) -> NEVER do
    loop do
        emit PIN (pin,true);
        await 1s;
        emit PIN (pin,false);
        await 1s;
    end
end

spawn Display(&horario, &alarme);
var int i;
var int identificarAcao;
var int isTimeOut = 0;
var bool alarmeOn = false;
loop i do
   alarmChange = false;
   identificarAcao = 
   do
    par do
      loop do
        await 10s; //Esperar 1 min
        call AumentaMinuto(&horario);
      end
    with
      loop do
        if (alarmeOn == true and alarme[0] == horario[0] and alarme[1] == horario[1] and alarme[2] == horario[2] and alarme[3] == horario[3]) then
          par/or do
            loop do
          //    emit PIN_03(false);
          //    await 300ms;
          //    emit PIN_03(true);
          //    await 1s;
              await Pisca(10); //substitui o buzzer nos testes
            end
          with
            par/and do
              await Segurou_botao(_A1);
            with
              await Segurou_botao(_A3);
            end
          end
          //modo soneca ativado
          var int j;
          loop j in [0 -> 5[ do
            call AumentaMinuto(&alarme);
          end
        end
        await 5s;
      end
    with //ajuste de horario
      await Segurou_botao(_A1); //ajustar horario -> segura botao 1 --> LED1 vai ficar piscando
      escape 1;
    with
      await Segurou_botao(_A2); //alarme ON/OFF -> segura botao 2 --> LED4 vai ficar fixo se o alarme estiver ligado
      emit PIN_10(not alarmeOn);
      escape 2;
    with
     await Segurou_botao(_A3); //ajustar alarme -> segura botao 3 --> LED3 vai ficar piscando
      escape 3;
    end
   end;

   if identificarAcao != 2 then
     var int led1;
     var int led2;
     var int led3;
     if identificarAcao == 1 then
         led1 = 13;
         led2 = 12;
         led3 = 11;
     else
         alarmChange = true;
         led1 = 11;
         led2 = 13;
         led3 = 12;
     end
     loop do
       par/or do
           await 5s; //se nao pressionar nada por 5 segundos, sai do estado
           continue/i;
       with
           await Pisca(led1);
       with
           await Segurou_botao(_A2); //segura botao 2 para ajustar as horas
           break;
       end
     end
     
     //modo de ajuste de hora --> LED1 fixo e LED2 piscando
      
     loop do
       par/or do
           await 5s; //se nao pressionar nada por 5 segundos, sai do estado
           if identificarAcao == 1 then
               emit PIN_13(true);
           else
               emit PIN_11(true);
           end
           continue/i;
       with
           if identificarAcao == 1 then
               emit PIN_13(false);
           else
               emit PIN_11(false);
           end
           await Pisca(led2);
       with
           var int pin;
           (pin,_) = await PIN_ until pin==_A1; //pressionar o botao 1 decrementa uma hora
           if identificarAcao == 1 then
               call DiminuiHora(&horario);
           else
               call DiminuiHora(&alarme);
           end
           await 500ms;
       with
           var int pin;
           (pin,_) = await PIN_ until pin==_A3; //pressionar o botao 3 acrescenta uma hora
           if identificarAcao == 1 then
               call AumentaHora(&horario);
           else
               call AumentaHora(&alarme);
           end
           await 500ms;
       with
           await Segurou_botao(_A2); //segura botao 2 para ajustar os minutos
           break;
       end
     end
     
     //modo de ajuste de minuto --> LED1 e LED2 fixos e LED3 piscando
      
     loop do
       par/or do
           await 5s; //se nao pressionar nada por 5 segundos, sai do estado
           if identificarAcao == 1 then
               emit PIN_12(true);
           else
               emit PIN_11(true);
           end
           emit PIN_13(true);
           continue/i;
       with
           emit PIN_13(false);
           if identificarAcao == 1 then
               emit PIN_12(false);
           else
               emit PIN_11(false);
           end
           await Pisca(led3);
       with
           var int pin;
           (pin,_) = await PIN_ until pin==_A1; //pressionar o botao 1 decrementa uma hora
           if identificarAcao == 1 then
               call DiminuiMinuto(&horario);
           else
               call DiminuiMinuto(&alarme);
           end
           await 500ms;
       with
           var int pin;
           (pin,_) = await PIN_ until pin==_A3; //pressionar o botao 3 acrescenta uma hora
           if identificarAcao == 1 then
               call AumentaMinuto(&horario);
           else
               call AumentaMinuto(&alarme);
           end
           await 500ms;
       end
     end
     
   else
     alarmeOn = not alarmeOn;
   end
end

#if 0
     
            //    isTimeOut = call ComparaVetores(&alarme, &horario);
            //    _ceu_arduino_assert(j==0,3);
            //    if isTimeOut == 1 then
           //         _ceu_arduino_assert(0,2);
            //    end
           // end     
           
           
                if alarmeOn == true then
                var int j;
                loop j in [0 -> 4[ do
                    if alarme[j] != horario[j] then
                        await FOREVER;
                    end
                end
                _ceu_arduino_assert(0,4);
                end
                await FOREVER;
     
#endif
